// Gradle 6.4.1

plugins {
  id 'java'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

sourceCompatibility = '14'
targetCompatibility = '14'

sourceSets {
  main {
    java {
      srcDirs = ['framework/src']
    }
  }

  grader {
    java {
      srcDirs = ['framework/tst']
    }
    compileClasspath += main.compileClasspath + main.output
    annotationProcessorPath += main.annotationProcessorPath
  }

  test {
    java {
      srcDirs = ['framework/tst-self']
    }
    compileClasspath += grader.compileClasspath + grader.output
    runtimeClasspath += grader.runtimeClasspath
    annotationProcessorPath += grader.annotationProcessorPath
  }

  labs {
    java {
      srcDirs = [ 'labs/lab0-pingpong/src',
                  'labs/lab1-clientserver/src',
                  'labs/lab2-primarybackup/src',
                  'labs/lab3-paxos/src',
                  'labs/lab4-shardedstore/src' ]
    }
    compileClasspath += main.compileClasspath + main.output
    annotationProcessorPath += main.annotationProcessorPath
  }

  labsTests {
    java {
      srcDirs = [ 'labs/lab0-pingpong/tst',
                  'labs/lab1-clientserver/tst',
                  'labs/lab2-primarybackup/tst',
                  'labs/lab3-paxos/tst',
                  'labs/lab4-shardedstore/tst']
    }
    compileClasspath += grader.compileClasspath + grader.output + labs.output
    annotationProcessorPath += grader.annotationProcessorPath
  }
}

configurations {
  sources {
    transitive = false
  }
  graderImplementation
  graderSources {
    transitive = false
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation      group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
  sources             group: 'org.projectlombok', name: 'lombok', version: '1.18.20', classifier: 'sources'

  implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
  sources        group: 'com.google.guava', name: 'guava', version: '29.0-jre', classifier: 'sources'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
  sources        group: 'org.apache.commons', name: 'commons-lang3', version: '3.10', classifier: 'sources'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.0'
  sources        group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.0', classifier: 'sources'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'
  sources        group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0', classifier: 'sources'

  graderImplementation group: 'junit', name: 'junit', version: '4.13'
  graderSources        group: 'junit', name: 'junit', version: '4.13', classifier: 'sources'
  graderImplementation group: 'io.github.kostaskougios', name: 'cloning', version: '1.10.3'
  graderSources        group: 'io.github.kostaskougios', name: 'cloning', version: '1.10.3', classifier: 'sources'

  graderImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: '2.11.0'
  graderSources        group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: '2.11.0', classifier: 'sources'
  graderImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.11.0'
  graderSources        group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.11.0', classifier: 'sources'
  graderImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.11.0'
  graderSources        group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.11.0', classifier: 'sources'
}

// Create reproducible jars
tasks.withType(AbstractArchiveTask) {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
}

task scaffoldingJar(type: ShadowJar) {
  archiveBaseName = 'framework'
  from sourceSets.main.output
}

task graderJar(type: ShadowJar) {
  archiveBaseName = 'grader'
  from sourceSets.grader.output
}

task scaffoldingSourcesJar(type: ShadowJar) {
  archiveBaseName = 'framework'
  classifier = 'sources'
  from sourceSets.main.java
}

task graderSourcesJar(type: ShadowJar) {
  archiveBaseName = 'grader'
  classifier = 'sources'
  from sourceSets.grader.java
}

task scaffoldingDependenciesJar(type: ShadowJar) {
  archiveBaseName = 'framework'
  classifier = 'deps'
  configurations = [ project.configurations.runtimeClasspath ]
}

task graderDependenciesJar(type: ShadowJar) {
  archiveBaseName = 'grader'
  classifier = 'deps'
  configurations = [ project.configurations.graderRuntimeClasspath ]
}

task scaffoldingDependenciesSourcesJar(type: ShadowJar) {
  archiveBaseName = 'framework'
  classifier = 'deps-sources'
  configurations = [ project.configurations.sources ]
}


jar.enabled = false

javadoc {
  title = 'Distributed Systems Labs Framework'
  destinationDir = file("$buildDir/doc")
  options.noTimestamp(true)
  options.noIndex(true)
  options.noDeprecatedList(true)
  options.noHelp(true)
  options.setOutputLevel(JavadocOutputLevel.QUIET)
}

tasks.assemble.dependsOn tasks.scaffoldingJar
tasks.assemble.dependsOn tasks.graderJar
tasks.assemble.dependsOn tasks.scaffoldingSourcesJar
tasks.assemble.dependsOn tasks.graderSourcesJar
tasks.assemble.dependsOn tasks.scaffoldingDependenciesJar
tasks.assemble.dependsOn tasks.graderDependenciesJar
tasks.assemble.dependsOn tasks.scaffoldingDependenciesSourcesJar

// copy dependencies into directories that IntelliJ can find
task copyDependencies {
  doLast {
    copy {
      from configurations.runtimeClasspath,
           configurations.sources
      into "deps/compile"
    }
    copy {
      from configurations.graderRuntimeClasspath,
           configurations.graderSources
      into "deps/test"
    }
  }
}

// Put gradle wrapper in a hidden folder
wrapper {
  jarFile = file('.gradle-wrapper/gradle-wrapper.jar')
}

// Run tests with illegal-access=deny to ensure that these warnings are caught
test.jvmArgs(['-ea',
              '--illegal-access=deny',
              '--add-opens', 'java.base/jdk.internal.reflect=ALL-UNNAMED',
              '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
              '--add-opens', 'java.base/java.util=ALL-UNNAMED',
              '--add-opens','java.base/java.util.concurrent.atomic=ALL-UNNAMED'])
